(ns audio-platform.screens.history
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.state :as state]
   [audio-platform.states.history :as history-state]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.cards :as cards]
   [audio-platform.widgets.common :as common]))

(defn ^m/Widget history-page [_context]
  (f/widget
   :get [m/Navigator]
   :watch [{loading :loading
            history :history} history-state/history-state]
   (m/Scaffold
    .appBar (m/AppBar
             .title (mgl/MongolText "History" .style (common/m-style))
             .leading (m/IconButton
                       .icon (m/Icon m.Icons/arrow_back)
                       .onPressed (fn [] (nav/navigator-pop navigator))))
    .body
    (common/async-view
     {:loading loading
      :is-empty? (empty? history)
      :empty-view (common/empty-state-view {:message "No history yet"})
      :content-fn
      (fn []
        (m/ListView.builder
         .scrollDirection m.Axis/horizontal
         .itemCount (count history)
         .itemBuilder (fn [_ index]
                        (let [item (nth history index)
                              ;; History usually returns a list of play records, which include the episode
                              episode (get item "episode" item)]
                          (cards/episode-card episode
                                              (fn []
                                                (state/set-current-episode episode)
                                                (nav/push-route navigator :player)))))
         ))}))))

