(ns audio-platform.screens.album-form
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.services.album :as album-service]
   [audio-platform.widgets.category-tree :as category-tree]
   [audio-platform.states.category :as category-state]
   [audio-platform.states.album :as album-state]
   [audio-platform.utils.toast :as toast]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.forms :as forms]
   [audio-platform.widgets.common :as common]))

(defn- cover-upload-section [uploading-cover upload-progress cover-url-controller ctx]
  (m/Row
   .crossAxisAlignment m.CrossAxisAlignment/start
   .children
   [(mgl/MongolTextField
     .controller cover-url-controller
     .decoration (m/InputDecoration .hintText "Enter cover image URL" .border (m/OutlineInputBorder))
     .style (common/m-style))
    (m/SizedBox .width 8)
    (mgl/MongolElevatedButton
     .child (mgl/MongolText (if @uploading-cover (str "Uploading... " (int (* @upload-progress 100)) "%") "Upload Image") .style (common/m-style))
     .onPressed
     (fn []
       (when (not @uploading-cover)
         (reset! uploading-cover true)
         (reset! upload-progress 0.0)
         (-> (album-service/upload-cover-image :on-progress (fn [p] (reset! upload-progress p)))
             (.then (fn [result]
                      (set! (.-text cover-url-controller) (:url result))
                      (reset! uploading-cover false)))
             (.catchError (fn [err]
                            (reset! uploading-cover false)
                            (toast/handle-error-with-toast ctx err "Зураг хуулахад алдаа гарлаа")))))))]))

(defn- category-selector [category-name ctx]
  (m/InkWell
   .onTap (fn [] (category-tree/show-dialog ctx))
   .child (m/Container
           .padding (m.EdgeInsets/all 12)
           .decoration (m/BoxDecoration .border (m/Border.all .color m/Colors.grey.shade400) .borderRadius (m.BorderRadius/circular 4))
           .child (mgl/MongolText (if (empty? category-name) "Select category" category-name)
                                  .style (common/m-style .color (if category-name m/Colors.blue m/Colors.grey.shade600))))))

(defn ^m/Widget album-form-page [_context]
  (let [album (album-state/get-album)
        is-edit (some? album)]
    (f/widget
     :watch [{loading :loading} album-state/album-state
             {category-name :selected-category-name} category-state/category-state]
     :context ctx
     :get [m/Navigator]
     :managed [title-controller (m/TextEditingController .text (if (some? album) (get album "title") ""))
               description-controller (m/TextEditingController .text (if (some? album) (get album "description") ""))
               cover-url-controller (m/TextEditingController .text (if (some? album) (get album "cover_url") ""))
               uploading-cover (atom false)
               upload-progress (atom 0.0)]
     (m/Scaffold
      .appBar (m/AppBar
               .leading (m/IconButton
                         .icon (m/Icon m.Icons/arrow_back)
                         .onPressed (fn [] (nav/navigator-pop navigator))))
      .body
      (m/SafeArea
       .child
       (m/SingleChildScrollView
        .scrollDirection m.Axis/horizontal
        .child
        (m/Padding
         .padding (m.EdgeInsets/all 16)
         .child
         (m/Row
          .crossAxisAlignment m.CrossAxisAlignment/start
          .children
          [(forms/field-label "Title *")
           (m/SizedBox .width 8)
           (mgl/MongolTextField
            .controller title-controller
            .decoration (m/InputDecoration .hintText "Enter album title" .border (m/OutlineInputBorder))
            .style (common/m-style))
           (m/SizedBox .width 16)
           (forms/field-label "Description")
           (m/SizedBox .width 8)
           (mgl/MongolTextField
            .controller description-controller
            .maxLines 5
            .decoration (m/InputDecoration .hintText "Enter album description" .border (m/OutlineInputBorder) .alignLabelWithHint true)
            .style (common/m-style))
           (m/SizedBox .width 16)
           (forms/field-label "Cover URL")
           (m/SizedBox .width 8)
           (cover-upload-section uploading-cover upload-progress cover-url-controller ctx)
           (m/SizedBox .width 16)
           (forms/field-label "Category")
           (m/SizedBox .width 8)
           (category-selector category-name ctx)
           (m/SizedBox .width 32)
           (forms/form-actions
            {:loading loading
             :submit-label (if is-edit "Save" "Create")
             :on-cancel (fn [] (nav/navigator-pop navigator))
             :on-submit (fn []
                          (let [title (.-text title-controller)
                                description (.-text description-controller)
                                cover-url (.-text cover-url-controller)]
                            (if (empty? title)
                              (toast/show-error-toast ctx "Гарчиг заавал байх ёстой")
                              (do
                                (swap! album-state/album-state assoc :loading true)
                                (swap! album-state/album-state assoc :error nil)
                                (let [album-data (cond-> {"title" title "description" description}
                                                   (not (empty? cover-url)) (assoc "cover_url" cover-url)
                                                   (some? (category-state/get-selected-category)) (assoc "category_id" (category-state/get-selected-category)))]
                                  (if is-edit
                                    (album-service/update-album navigator (get album "id") album-data)
                                    (album-service/create-album navigator album-data)))))))})]))))))))
