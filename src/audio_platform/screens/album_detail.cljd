(ns audio-platform.screens.album-detail
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.state :as state]
   [audio-platform.states.album :as album-state]
   [audio-platform.states.episode :as episode-state]
   [audio-platform.services.episode :as episode-service]
   [audio-platform.services.comment :as comment-service]
   [audio-platform.services.audio-player :as audio-player]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.common :as common]
   [audio-platform.widgets.items :as items]
   [audio-platform.widgets.app-bar :as app-bar]))

(defn ^m/Widget album-detail-page [_context]
  (let [album (album-state/get-album)
        current-user (state/get-user)
        album-id (get album "id")
        is-creator (= (get album "creator_id") (get current-user "id"))]
    (f/widget
     :context ctx
     :get [m/Navigator]
     :watch [{loading :loading
              error :error
              album :album} album-state/album-state
             {episodes :episodes} episode-state/episode-state]
     (m/Scaffold
       .appBar (app-bar/album-detail-app-bar {:album album :album-id album-id :is-creator is-creator :navigator navigator :ctx ctx})
       .body (m/SafeArea
              .child (common/async-view
                      {:loading loading
                       :error error
                       :content-fn
                       (fn []
                         (m/ListView.separated
                          .scrollDirection m/Axis.horizontal
                          .padding (m/EdgeInsets.symmetric .horizontal 16)
                          .itemCount (+ 1 (if (empty? episodes) 1 (count episodes)))
                          .itemBuilder (fn [_ index]
                                         (if (= index 0)
                                           (items/album-info-item album episodes)
                                            (if (empty? episodes)
                                              (m/Center
                                               .child
                                               (m/Container
                                                .width 300
                                                .child
                                                (common/empty-state-view
                                                 {:icon m/Icons.queue_music_rounded
                                                  :message "ᠣᠩᠭᠤᠴᠠ ᠪᠠᠶᠢᠬᠤ ᠦᠭᠡᠢ" ;; No episodes yet
                                                  :action-child (when is-creator
                                                                  (m/ElevatedButton
                                                                   .style (m/ElevatedButton.styleFrom
                                                                           .padding (m/EdgeInsets.symmetric .vertical 20)
                                                                           .shape (m/RoundedRectangleBorder .borderRadius (m/BorderRadius.circular 16)))
                                                                   .child (mgl/MongolText "ᠰᠢᠨᠡ ᠠᠩᠭᠢ ᠨᠢᠮᠡᠬᠦ" .style (common/m-style)) ;; Add New Episode
                                                                   .onPressed (fn []
                                                                                (episode-state/reset-state)
                                                                                (episode-state/set-episode nil)
                                                                                (nav/push-route navigator :episode-form))))})))
                                             (items/episode-item (nth episodes (- index 1)) is-creator
                                                                 {:on-edit (fn [episode]
                                                                             (episode-state/set-episode episode)
                                                                             (nav/push-route navigator :episode-form))
                                                                  :on-delete (fn [episode]
                                                                               (common/show-confirmation-dialog
                                                                                ctx
                                                                                {:message "Are you sure you want to delete this episode? This action cannot be undone."
                                                                                 :on-confirm #(episode-service/delete-episode navigator (get episode "id") album-id)}))
                                                                  :on-play (fn [episode]
                                                                             (audio-player/play-episode episode))
                                                                  :on-tap (fn [episode]
                                                                            (let [ep-id (get episode "id")]
                                                                              (episode-state/set-episode episode)
                                                                              (comment-service/get-comments ep-id)
                                                                              (episode-service/check-favorite ep-id)
                                                                              (nav/push-route navigator :episode-detail)))}))))
                          .separatorBuilder (fn [_ _] (m/SizedBox .width 8))))}))))))
