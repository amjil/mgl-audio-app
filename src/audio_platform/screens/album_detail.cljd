(ns audio-platform.screens.album-detail
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.state :as state]
   [audio-platform.states.album :as album-state]
   [audio-platform.states.episode :as episode-state]
   [audio-platform.services.episode :as episode-service]
   [audio-platform.services.album :as album-service]
   [audio-platform.services.audio-player :as audio-player]
   [audio-platform.utils.format :as format]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.common :as common]))

(defn episode-item [episode ctx navigator album-id is-creator]
  (mgl/MongolListTile
   .leading (m/Icon m/Icons.play_circle_outline)
   .title (mgl/MongolText
           (get episode "title" "Unknown Episode")
           .style (common/m-style .fontSize 16 .fontWeight m/FontWeight.normal))
   .subtitle (mgl/MongolText
              (format/format-duration (get episode "duration" 0))
              .style (common/m-style .color m/Colors.grey.shade600 .fontSize 12))
   .trailing (m/Row
              .mainAxisSize m/MainAxisSize.min
              .children
              (if is-creator
                [(m/IconButton
                  .icon (m/Icon m/Icons.edit)
                  .onPressed (fn []
                               (episode-state/set-episode episode)
                               (nav/push-route navigator :episode-form)))
                 (m/IconButton
                  .icon (m/Icon m/Icons.delete)
                  .color m/Colors.red
                  .onPressed (fn []
                               (common/show-confirmation-dialog
                                ctx
                                {:message "Are you sure you want to delete this episode? This action cannot be undone."
                                 :on-confirm #(episode-service/delete-episode navigator (get episode "id") album-id)})))
                 (m/IconButton
                  .icon (m/Icon m/Icons.play_arrow)
                  .onPressed (fn []
                               (audio-player/play-episode episode)))]
                [(m/IconButton
                  .icon (m/Icon m/Icons.play_arrow)
                  .onPressed (fn []
                               (audio-player/play-episode episode)))]))
   .onTap (fn []
            (episode-state/set-episode episode)
            (nav/push-route navigator :episode-detail))))

(defn- album-info-item [album episodes]
  (m/Padding
   .padding (m.EdgeInsets/all 16)
   .child
   (m/Column
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(m/Container
      .height 160
      .decoration (m/BoxDecoration .color m/Colors.grey.shade300)
      .child (if (get album "cover_url")
               (m/Image.network (get album "cover_url") .fit m/BoxFit.cover)
               (m/Center .child (m/Icon m/Icons.album .size 64 .color m/Colors.grey.shade600))))
     (m/SizedBox .height 20)
     (m/Row
      .children
      [(mgl/MongolText (get album "title" "Unknown Album")
                      .style (common/m-style .fontSize 24 .fontWeight m/FontWeight.bold))
       (m/SizedBox .width 4)
       (mgl/MongolText (get album "description" "")
                      .style (common/m-style .fontSize 14 .color m/Colors.grey.shade700))
       (m/SizedBox .width 4)
       (m/VerticalDivider)
       (m/SizedBox .width 4)
       (mgl/MongolText (str "Episodes (" (count episodes) ")")
                      .style (common/m-style .fontSize 18 .fontWeight m/FontWeight.bold))])])))

(defn- album-app-bar [album album-id is-creator navigator ctx]
  (m/AppBar
   .leading (m/IconButton
             .icon (m/Icon m/Icons.arrow_back)
             .onPressed (fn [] (nav/navigator-pop navigator)))
   .actions (let [favorite-btn (m/IconButton
                                .icon (m/Icon (if (get album "is_favorite") m/Icons.favorite m/Icons.favorite_border))
                                .color (if (get album "is_favorite") m/Colors.red nil)
                                .onPressed (fn []
                                             (if (get album "is_favorite")
                                               (album-service/remove-favorite album-id)
                                               (album-service/add-favorite album-id))))]
              (if is-creator
                [favorite-btn
                 (m/IconButton
                  .icon (m/Icon m/Icons.add)
                  .tooltip "Add Episode"
                  .onPressed (fn []
                               (episode-state/reset-state)
                               (episode-state/set-episode nil)
                               (nav/push-route navigator :episode-form)))
                 (m/IconButton
                  .icon (m/Icon m/Icons.edit)
                  .onPressed (fn []
                               (nav/push-route navigator :album-form)))
                 (m/IconButton
                  .icon (m/Icon m/Icons.delete)
                  .onPressed (fn []
                               (common/show-confirmation-dialog
                                ctx
                                {:message "Are you sure you want to delete this album? This action cannot be undone."
                                 :on-confirm #(album-service/delete-album navigator album-id)})))]
                [favorite-btn]))))

(defn ^m/Widget album-detail-page [_context]
  (let [album (album-state/get-album)
        current-user (state/get-user)
        album-id (get album "id")
        is-creator (= (get album "creator_id") (get current-user "id"))]
    (f/widget
     :context ctx
     :get [m/Navigator]
     :watch [{loading :loading
              error :error
              album :album} album-state/album-state
             {episodes :episodes} episode-state/episode-state]
     :init (when album-id (album-service/check-favorite album-id))
     (m/Scaffold
      .appBar (album-app-bar album album-id is-creator navigator ctx)
      .body (m/SafeArea
             .child (common/async-view
                     {:loading loading
                      :error error
                      :content-fn
                      (fn []
                        (m/ListView.separated
                         .scrollDirection m.Axis/horizontal
                         .itemCount (+ 1 (if (empty? episodes) 1 (count episodes)))
                         .itemBuilder (fn [_ index]
                                        (if (= index 0)
                                          (album-info-item album episodes)
                                          (if (empty? episodes)
                                            (common/empty-state-view
                                             {:icon m/Icons.queue_music
                                              :message "No episodes yet"
                                              :action-child (when is-creator
                                                              (mgl/MongolElevatedButton
                                                               .child (mgl/MongolText "Add First Episode" .style (common/m-style))
                                                               .onPressed (fn []
                                                                            (episode-state/reset-state)
                                                                            (episode-state/set-episode nil)
                                                                            (nav/push-route navigator :episode-form))))})
                                            (episode-item (nth episodes (- index 1)) ctx navigator album-id is-creator))))
                         .separatorBuilder (fn [_ _] (m/VerticalDivider .width 1 .thickness 0))))}))))))
