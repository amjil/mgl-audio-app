(ns audio-platform.screens.episode-form
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.services.episode :as episode-service]
   [audio-platform.states.episode :as episode-state]
   [audio-platform.states.album :as album-state]
   [audio-platform.utils.toast :as toast]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.forms :as forms]
   [audio-platform.widgets.common :as common]))

(defn- audio-upload-section [uploading-atom progress-atom selected-atom lock-atom media-id-controller ctx]
  (f/widget
   :watch [uploading uploading-atom
           progress progress-atom
           selected selected-atom]
   (m/Row
    .crossAxisAlignment m.CrossAxisAlignment/start
    .children
    [(mgl/MongolElevatedButton
      .child (mgl/MongolText (if uploading
                               (str "Uploading... " (int (* progress 100)) "%")
                               "Select Audio File")
                             .style (common/m-style))
      .onPressed
      (when (not (or uploading @lock-atom))
        (fn []
          (dart:core/print "Select Audio File button pressed!")
          (reset! lock-atom true)
          (reset! uploading-atom true)
          (reset! progress-atom 0.0)
          (-> (episode-service/upload-audio-file :on-progress (fn [p] (reset! progress-atom p)))
              (.then (fn [result]
                       (let [media-id (:media-id result)
                             file-info (:file-info result)]
                         (set! (.-text media-id-controller) media-id)
                         (reset! selected-atom file-info)
                         (reset! uploading-atom false)
                         (reset! lock-atom false))))
              (.catchError (fn [err]
                               (reset! uploading-atom false)
                               (reset! lock-atom false)
                               (toast/handle-error-with-toast ctx err "Файл хуулахад алдаа гарлаа")))))))
     (m/SizedBox .width 8)
     (if uploading
       (m/SizedBox .width 100 .child (m/LinearProgressIndicator .value progress))
       (m/SizedBox))
     (m/SizedBox .width 8)
     (if (some? selected)
       (mgl/MongolText (str "Selected: " (:name selected))
                       .style (common/m-style :fontSize 12 :color m/Colors.green))
       (m/SizedBox)) ])))

(defn ^m/Widget episode-form-page [_context]
  (let [episode (episode-state/get-episode)
        album (album-state/get-album)
        album-id (get album "id")
        selected-file (atom nil)
        uploading (atom false)
        upload-progress (atom 0.0)
        picker-lock (atom false)]
    (f/widget
     :watch [{loading :loading} episode-state/episode-state
             is-uploading uploading]
     :context ctx
     :get [m/Navigator]
     :managed [title-controller (m/TextEditingController .text (if (some? episode) (get episode "title") ""))
               media-id-controller (m/TextEditingController .text "")
               sequence-controller (m/TextEditingController .text (if (some? episode) (str (or (get episode "sequence") "")) ""))]
     (m/Scaffold
      .appBar (m/AppBar
               .leading (m/IconButton
                         .icon (m/Icon m.Icons/arrow_back)
                         .onPressed (fn [] (episode-state/reset-state) (nav/navigator-pop navigator))))
      .body
      (m/SafeArea
       .child
       (m/SingleChildScrollView
        .scrollDirection m.Axis/horizontal
        .child
        (m/Padding
         .padding (m.EdgeInsets/all 16)
         .child
         (m/Row
          .crossAxisAlignment m.CrossAxisAlignment/start
          .children
          [(forms/field-label "Title *")
           (m/SizedBox .width 8)
           (mgl/MongolTextField
            .controller title-controller
            .decoration (m/InputDecoration .hintText "Enter episode title" .border (m/OutlineInputBorder))
            .style (common/m-style))
           (m/SizedBox .width 16)
           (forms/field-label "Audio File *")
           (m/SizedBox .width 8)
           (audio-upload-section uploading upload-progress selected-file picker-lock media-id-controller ctx)
           (m/SizedBox .width 8)
           (mgl/MongolTextField
            .controller media-id-controller
            .decoration (m/InputDecoration .hintText "Media ID (auto-filled after upload)" .border (m/OutlineInputBorder) .enabled (not is-uploading))
            .style (common/m-style))
           (m/SizedBox .width 16)
           (forms/field-label "Sequence")
           (m/SizedBox .width 8)
           (mgl/MongolTextField
            .controller sequence-controller
            .keyboardType m.TextInputType/number
            .decoration (m/InputDecoration .hintText "Enter sequence number" .border (m/OutlineInputBorder))
            .style (common/m-style))
           (m/SizedBox .width 32)
           (forms/form-actions
            {:loading loading
             :on-cancel (fn [] (episode-state/reset-state) (nav/navigator-pop navigator))
             :on-submit (fn []
                          (when (and (not loading) (not is-uploading))
                            (let [title (.-text title-controller)
                                  media-id (.-text media-id-controller)
                                  sequence-str (.-text sequence-controller)]
                              (cond
                                (empty? title) (toast/show-error-toast ctx "Гарчиг заавал байх ёстой")
                                (empty? media-id) (toast/show-error-toast ctx "Аудио файл хуулна uу")
                                :else
                                (do
                                  (swap! episode-state/episode-state assoc :loading true)
                                  (let [episode-data (cond-> {"title" title "media_id" media-id}
                                                       (not (empty? sequence-str)) (assoc "sequence" (dart:core/int.parse sequence-str)))]
                                    (episode-service/create-episode navigator album-id episode-data)))))))})]))))))))
