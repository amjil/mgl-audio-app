(ns audio-platform.screens.home
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [audio-platform.widgets.cards :as cards]
   [audio-platform.states.home :as home-state]
   [audio-platform.state :as state]
   [audio-platform.states.album :as album-state]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.services.album :as album-service]))


(defn home-page [_context]
  ;; Load data on mount
  (home-state/init-home-data!)
  (f/widget
   :watch [{feed-items :items
            loading :loading} home-state/home-feed-state]
   :get [m/Navigator]
   (m/Scaffold
    .appBar (m/AppBar
             .actions
             [(m/IconButton
               .icon (m/Icon m.Icons/add_circle_outline)
               .tooltip "Create Album"
               .onPressed (fn []
                            (album-state/reset-state)
                            (album-state/set-album nil)
                            (album-service/get-categories :page 1 :per-page 100)
                            (nav/push-route navigator :album-form)))
              (m/IconButton
               .icon (m/Icon m.Icons/search)
               .onPressed (fn []
                            (nav/push-route navigator :search)))
              (m/IconButton
               .icon (m/Icon m.Icons/notifications)
               .onPressed (fn []
                            (dart:core/print "Notifications")))])
    .body (if loading
            (m/Center
             .child (m/CircularProgressIndicator))
            (m/ListView.separated
             .scrollDirection m.Axis/horizontal
             .padding (m.EdgeInsets/only .top 12 .bottom 24)
             .itemCount (count feed-items)
             .separatorBuilder (fn [_ __] (m/VerticalDivider .width 1 .thickness 0))
             .itemBuilder (fn [_ index]
                            (let [item (nth feed-items index)
                                  item-type (get item "type")]
                              (cards/feed-card item
                                               (fn []
                                                 (album-state/set-album item)
                                                 (case item-type
                                                   "album" (nav/push-route-with-arguments navigator
                                                                                          :album-detail
                                                                                          {"album_id" (get item "id")})
                                                   ;; For episode, store current data; playback/detail can be added later
                                                   "episode" (state/set-current-episode item)
                                                   (dart:core/print (str "Unknown type:" item-type))))))))))))

