(ns audio-platform.screens.home
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.states.home :as home-state]
   [audio-platform.state :as state]
   [audio-platform.states.font :as font-state]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.utils.format :as format]))

(defn episode-card [episode on-tap]
  (f/widget
   :watch [_font-state font-state/font-state]
   :let [font-family (font-state/get-app-font-family)]
   (m/Card
    .margin (m.EdgeInsets/symmetric .horizontal 8 .vertical 4)
    .child (m/InkWell
            .onTap on-tap
            .child (m/Padding
                    .padding (m.EdgeInsets/all 12)
                    .child (m/Row
                            .children
                            [(m/Expanded
                              .child (m/Column
                                      .crossAxisAlignment m.CrossAxisAlignment/start
                                      .children
                                      [(mgl/MongolText
                                        (get episode "title" "Unknown Title")
                                        .style (m/TextStyle
                                                .fontFamily font-family
                                                .fontSize 16
                                                .fontWeight m.FontWeight/bold))
                                       (m/SizedBox .height 4)
                                       (mgl/MongolText
                                        (format/format-duration (get episode "duration" 0))
                                        .style (m/TextStyle
                                                .fontFamily font-family
                                                .color m/Colors.grey.shade600
                                                .fontSize 12))]))
                             (m/IconButton
                              .icon (m/Icon m.Icons/play_circle_outline)
                              .onPressed on-tap)]))))))

(defn album-card [album on-tap]
  (f/widget
   :watch [_font-state font-state/font-state]
   :let [font-family (font-state/get-app-font-family)]
   (m/Card
    .margin (m.EdgeInsets/all 8)
    .child (m/InkWell
            .onTap on-tap
            .child (m/Column
                    .crossAxisAlignment m.CrossAxisAlignment/start
                    .children
                    [(m/AspectRatio
                      .aspectRatio 1.0
                      .child (m/Container
                              .decoration (m/BoxDecoration
                                           .color m/Colors.grey.shade300
                                           .borderRadius (m.BorderRadius/only
                                                          .topLeft (m.Radius/circular 4)
                                                          .topRight (m.Radius/circular 4)))
                              .child (if (get album "cover_url")
                                       (m/Image.network (get album "cover_url")
                                                        .fit m.BoxFit/cover)
                                       (m/Icon m.Icons/album
                                               .size 64
                                               .color m/Colors.grey.shade600))))
                     (m/Padding
                      .padding (m.EdgeInsets/all 12)
                      .child (m/Column
                              .crossAxisAlignment m.CrossAxisAlignment/start
                              .children
                              [(mgl/MongolText
                                (get album "title" "Unknown Album")
                                .style (m/TextStyle
                                        .fontFamily font-family
                                        .fontSize 16
                                        .fontWeight m.FontWeight/bold)
                                .maxLines 2
                                .overflow m.TextOverflow/ellipsis)
                               (m/SizedBox .height 4)
                               (mgl/MongolText
                                (get album "description" "")
                                .style (m/TextStyle
                                        .fontFamily font-family
                                        .color m/Colors.grey.shade600
                                        .fontSize 12)
                                .maxLines 2
                                .overflow m.TextOverflow/ellipsis)]))])))))

(defn home-page [_context]
  ;; Load data on mount
  (home-state/init-home-data!)
  (f/widget
   :watch [_font-state font-state/font-state
           _home-feed-state home-state/home-feed-state
           _albums-state home-state/albums-state]
   :get [m/Navigator]
   :let [font-family (font-state/get-app-font-family)
         loading (home-state/is-loading?)
         feed-items (home-state/get-home-feed-items)
         albums (home-state/get-albums)]
   (m/Scaffold
    .appBar (m/AppBar
             .actions
             [(m/IconButton
               .icon (m/Icon m.Icons/add_circle_outline)
               .tooltip "Create Album"
               .onPressed (fn []
                            (nav/push-route navigator :album-form)))
              (m/IconButton
               .icon (m/Icon m.Icons/search)
               .onPressed (fn []
                            (nav/push-route navigator :search)))
              (m/IconButton
               .icon (m/Icon m.Icons/notifications)
               .onPressed (fn []
                            (dart:core/print "Notifications")))])
    .body (if loading
            (m/Center
             .child (m/CircularProgressIndicator))
            (m/RefreshIndicator
             .onRefresh (fn []
                          (home-state/refresh-home-feed!)
                          (home-state/refresh-albums!)
                          (dart:core/Future.delayed (dart:core/Duration .seconds 1) (fn [])))
             .child (m/SingleChildScrollView
                     .child (m/Column
                             .crossAxisAlignment m.CrossAxisAlignment/start
                             .children
                             [(m/Padding
                               .padding (m.EdgeInsets/all 16)
                               .child (mgl/MongolText
                                       "Recommended Albums"
                                       .style (m/TextStyle
                                               .fontFamily font-family
                                               .fontSize 20
                                               .fontWeight m.FontWeight/bold)))
                              (m/SizedBox
                               .height 200
                               .child (m/ListView
                                       .scrollDirection m.Axis/horizontal
                                       .children (map (fn [album]
                                                        (album-card album
                                                                    (fn []
                                                                      (nav/push-route-with-arguments navigator
                                                                                                     :album-detail
                                                                                                     {"album_id" (get album "id")}))))
                                                      albums)))
                              (m/Padding
                               .padding (m.EdgeInsets/all 16)
                               .child (mgl/MongolText
                                       "Recommended Content"
                                       .style (m/TextStyle
                                               .fontFamily font-family
                                               .fontSize 20
                                               .fontWeight m.FontWeight/bold)))
                              (m/Column
                               .children (map (fn [episode]
                                                (episode-card episode
                                                              (fn []
                                                                (state/set-current-episode episode)
                                                                (dart:core/print (str "Episode tapped:" (get episode "id"))))))
                                              feed-items))])))))))

