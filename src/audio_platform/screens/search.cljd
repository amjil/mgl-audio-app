(ns audio-platform.screens.search
  (:require
   ["package:flutter/material.dart" :as m]
   [cljd.flutter :as f]
   [virtual-keyboard.keyboard :as keyboard]
   [audio-platform.state :as state]
   [audio-platform.services.search :as search-service]
   [audio-platform.states.search :as search-state]
   [audio-platform.utils.navigator :as nav]
   [audio-platform.widgets.cards :as cards]
   [audio-platform.services.album :as album-service]
   [audio-platform.widgets.common :as common]
   [audio-platform.widgets.items :as items]
   [audio-platform.widgets.forms :as forms]))

(defn search-results-list [results _selected-type]
  (f/widget
   :get [m/Navigator]
   (cond
     (empty? results)
     (common/empty-state-view
      {:icon m.Icons/search_off
       :message "No results found"})
     
     :else
     (m/ListView.builder
      .itemCount (count results)
      .itemBuilder (fn [_ index]
                     (let [item (nth results index)
                           item-type (get item "type")
                           on-tap (fn []
                                    (case item-type
                                      "album"
                                      (do
                                        (album-service/prefill-category-from-album-id (get item "id"))
                                        (nav/push-route-with-arguments navigator
                                                                       :album-detail
                                                                       {"album_id" (get item "id")}))
                                      "episode"
                                      (state/set-current-episode item)
                                      "creator"
                                      (dart:core/print (str "Creator tapped:" (get item "id")))
                                      (dart:core/print (str "Unknown type:" item-type))))]
                       (case item-type
                         "album"
                         (cards/album-card item on-tap)
                         "episode"
                         (cards/episode-card item on-tap)
                         "creator"
                         (cards/creator-card item on-tap)
                         (cards/feed-card item on-tap))))))))

(defn- search-categories [albums episodes creators selected-type]
  (m/Container
   .decoration (m/BoxDecoration
                .border (m/Border
                         .right (m/BorderSide
                                 .color m/Colors.grey.shade300
                                 .width 1)))
   .child (m/Column
           .children
           [(items/category-tab-item "Albums" (count albums) (= selected-type "albums")
                                     (fn [] (search-state/set-selected-type "albums")))
            (items/category-tab-item "Episodes" (count episodes) (= selected-type "episodes")
                                     (fn [] (search-state/set-selected-type "episodes")))
            (items/category-tab-item "Creators" (count creators) (= selected-type "creators")
                                     (fn [] (search-state/set-selected-type "creators")))])))

(defn search-results-panel []
  (f/widget
   :watch [{loading :loading
            albums :albums
            episodes :episodes
            creators :creators
            selected-type :selected-type
            query :query} search-state/search-state]
   :let [current-results (case selected-type
                           "albums" albums
                           "episodes" episodes
                           "creators" creators
                           albums)]
   (cond
     loading
     (common/loading-view)
     
     (empty? query)
     (common/empty-state-view
      {:icon m.Icons/search
       :message "Enter keywords to search audio content"})
     
     :else
     (m/Row
      .crossAxisAlignment m.CrossAxisAlignment/stretch
      .children
      [(search-categories albums episodes creators selected-type)
       (m/Expanded
        .child (search-results-list current-results selected-type))]))))

(defn ^m/Widget search-page [_context]
  (f/widget
   :get [m/Navigator]
   :managed [search-controller (m/TextEditingController.)]
   (m/Scaffold
    .backgroundColor (-> m/Theme (.of _context) .-colorScheme .-surface)
    .appBar (m/AppBar
             .backgroundColor m/Colors.transparent
             .leading (m/IconButton
                       .icon (m/Icon m.Icons/arrow_back)
                       .onPressed (fn [] (nav/navigator-pop navigator))))
    .body
    (m/SafeArea
     .child
     (m/Column
      .children
      [(m/Expanded
        .child (m/Stack
                .children
                [(m/Row
                  .crossAxisAlignment m.CrossAxisAlignment/stretch
                  .children
                  [(forms/search-input-field {:controller search-controller
                                              :context _context
                                              :on-changed (fn [query] (search-service/search query))})
                   (m/Expanded
                    .child (search-results-panel))])
                 (keyboard/candidates nil)]))
       (keyboard/keyboard {:custom-fns {}})])))))
