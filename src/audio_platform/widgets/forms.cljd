(ns audio-platform.widgets.forms
  (:require
   ["package:flutter/material.dart" :as m]
   ["package:mongol/mongol.dart" :as mgl]
   [cljd.flutter :as f]
   [audio-platform.widgets.common :as common]))

(defn field-label [label]
  (f/widget
   (mgl/MongolText label
                    .style (common/m-style .fontSize 14 .fontWeight m.FontWeight/bold))))

(defn form-actions [{:keys [loading submit-label on-submit on-cancel]}]
  (m/Row
   .mainAxisAlignment m.MainAxisAlignment/end
   .children
   [(mgl/MongolTextButton
     .child (mgl/MongolText "Cancel" .style (common/m-style))
     .onPressed on-cancel)
    (m/SizedBox .width 16)
    (mgl/MongolElevatedButton
     .child (if loading
              (m/SizedBox .width 20 .height 20 .child (m/CircularProgressIndicator .strokeWidth 2 .valueColor (m/AlwaysStoppedAnimation m/Colors.white)))
              (mgl/MongolText (or submit-label "Submit") .style (common/m-style)))
     .onPressed (if loading nil on-submit))]))

(defn search-input-field [{:keys [controller context on-changed]}]
  (m/Container
   .margin (m.EdgeInsets/only .right 4 .bottom 12)
   .padding (m.EdgeInsets/all 12)
   .decoration (m/BoxDecoration
                .border (m.Border/all)
                .borderRadius (m.BorderRadius/circular 12))
   .child (mgl/MongolTextField
           .controller controller
           .maxLines 1
           .autofocus true
           .onChanged (fn [query]
                        (when on-changed (on-changed query)))
           .style (common/m-style
                   .fontSize 20
                   .color (-> m/Theme (.of context) .-colorScheme .-onSurface))
           .decoration nil)))

